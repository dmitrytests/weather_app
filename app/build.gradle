apply plugin: 'com.android.application'

def getApiKey(){
    def Properties props = new Properties()
    props.load(new FileInputStream(new File('secret.properties')))
    return props['API_KEY']
}

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "com.kanemara.weather.weatherapp"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField ("String", "API_KEY", "\""+getApiKey()+"\"")
        buildConfigField ("String", "BASE_URL", "\"http://api.openweathermap.org/data/2.5/\"")
        resValue "string", "app_name", "WeatherApp"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions.incremental = false

    repositories {
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }

    flavorDimensions "default"

    productFlavors {
        kiev {
            dimension "default"
            applicationIdSuffix ".kiev"
            buildConfigField "int", "DEFAULT_CITY_ID", "703448"
            resValue "string", "app_name", "KievWeather"
        }
        lviv {
            dimension "default"
            applicationIdSuffix ".lviv"
            buildConfigField "int", "DEFAULT_CITY_ID", "702550"
            resValue "string", "app_name", "LvivWeather"
        }

    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation  'com.google.code.gson:gson:2.8.2'
    implementation 'com.squareup.retrofit2:retrofit:2.1.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.1.0'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.10'
    implementation 'com.google.dagger:dagger:2.10'
    compileOnly 'javax.annotation:jsr250-api:1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
